buildscript {
  ext.kotlin_version = '1.1.3-2'
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version",
      'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4',
      'org.springframework.boot:spring-boot-gradle-plugin:1.5.4.RELEASE'
  }
}

[
  'org.junit.platform.gradle.plugin',
  'kotlin',
  'org.springframework.boot'
].forEach { apply plugin: it }

repositories {
  mavenCentral()
  jcenter()
}

version = '1.0.0-SNAPSHOT'

dependencies {
  compile 'org.apache.httpcomponents:httpclient',
    'com.fasterxml.jackson.datatype:jackson-datatype-jsr310',
    'com.fasterxml.jackson.module:jackson-module-kotlin',
    'com.google.guava:guava:22.0',
    "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version",
    'org.springframework.boot:spring-boot-starter-actuator',
    'org.springframework.boot:spring-boot-starter-integration',
    'org.springframework.boot:spring-boot-starter-web',
    'org.springframework.boot:spring-boot-starter-websocket',
    'org.springframework.integration:spring-integration-http',
    'org.springframework.integration:spring-integration-websocket'

  testCompile 'org.amshove.kluent:kluent:1.26',
    'org.junit.platform:junit-platform-runner:1.0.0-M4'
  testCompile('org.jetbrains.spek:spek-api:1.1.2') {
    exclude group: 'org.jetbrains.kotlin'
  }
  testCompile('org.jetbrains.spek:spek-junit-platform-engine:1.1.2') {
    exclude group: 'org.junit.platform'
    exclude group: 'org.jetbrains.kotlin'
  }
}

processResources {
  expand(project.properties)
}

junitPlatform {
  filters {
    engines {
      include 'spek'
    }
  }
}

jar {
  manifest {
    attributes(
      "Implementation-Title": rootProject.name,
      "Implementation-Version": version
    )
  }
}

bootRepackage {
  enabled = false
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}
